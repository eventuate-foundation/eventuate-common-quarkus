buildscript {
    repositories {
        mavenCentral()
        maven {
          url "https://plugins.gradle.org/m2/"
        }
        eventuateMavenRepoUrl.split(',').each { repoUrl -> maven { url repoUrl } }
    }
    dependencies {
        classpath "com.avast.gradle:gradle-docker-compose-plugin:$dockerComposePluginVersion"
        classpath "io.eventuate.plugins.gradle:eventuate-plugins-gradle-publish:$eventuatePluginsGradleVersion"
    }
}

plugins {
}

apply plugin: 'docker-compose'
apply plugin: "io.eventuate.plugins.gradle.publish.EventuatePublish"

allprojects {
    group = "io.eventuate.common"
}

projects {

}

subprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        jcenter()
        eventuateMavenRepoUrl.split(',').each { repoUrl -> maven { url repoUrl } }
    }

    dependencies {
        testImplementation "junit:junit:4.12"
    }
}

task aggregatedTestReport(type: TestReport) {
  destinationDir = file("$buildDir/reports/allTests")
  // Include the results from the 'test' task in all subprojects
  reportOn subprojects*.test
}

gradle.projectsEvaluated {
    task aggregateJavaDocs(type: Javadoc) {
        description = 'Aggregates Javadoc API documentation of all subprojects.'
        group = JavaBasePlugin.DOCUMENTATION_GROUP

        destinationDir file("$buildDir/docs/javadoc")
    }
}

dockerCompose {
    removeOrphans = true

    environment.put "EVENTUATE_COMMON_VERSION", eventuateCommonImageVersion

    mysql8 {
        projectName = null
        useComposeFiles = ["docker-compose-mysql8.yml", "docker-compose-zookeeper.yml"]
        removeContainers = Boolean.valueOf(project.ext.removeContainers)
    }

    postgres {
        projectName = null
        useComposeFiles = ["docker-compose-postgres.yml", "docker-compose-zookeeper.yml"]
        removeContainers = Boolean.valueOf(project.ext.removeContainers)
    }

    mssql {
        projectName = null
        useComposeFiles = ["docker-compose-mssql.yml", "docker-compose-zookeeper.yml"]
        removeContainers = Boolean.valueOf(project.ext.removeContainers)
    }
}
